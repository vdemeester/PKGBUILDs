# $Id: PKGBUILD 176354 2013-01-30 20:33:38Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xkeyboard-config
pkgver=2.8
pkgrel=1
pkgdesc="X keyboard configuration files"
arch=(any)
license=('custom')
url="http://www.freedesktop.org/wiki/Software/XKeyboardConfig"
depends=('xorg-xkbcomp')
makedepends=('intltool')
provides=('xkbdata')
replaces=('xkbdata')
conflicts=('xkbdata')
source=(http://xorg.freedesktop.org/archive/individual/data/${pkgname}/${pkgname}-${pkgver}.tar.bz2 fr evdev.xml base.xml)
sha256sums=('4e1cf5468d488aaf320ddfd439e03cbda0b213550fd734026d23138e4b2078ae' '7fb7d04f40feb3fef9f85e99d824bcc09fd5c957d772ad14f3ebbb90dd4ffa41' '121d69e304ff247243d56ec79d73508c056850f1073f2ff8e12dd2372f87a43f' '121d69e304ff247243d56ec79d73508c056850f1073f2ff8e12dd2372f87a43f')

build() {
  cp ./fr "${srcdir}/${pkgname}-${pkgver}/symbols/"
  cp ./evdev.xml "${srcdir}/${pkgname}-${pkgver}/rules/"
  cp ./base.xml "${srcdir}/${pkgname}-${pkgver}/rules/"
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --with-xkb-base=/usr/share/X11/xkb \
      --with-xkb-rules-symlink=xorg \
      --enable-compat-rules=yes
  make
 }
 
 package() { 
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/share/X11/xkb/compiled"
  install -m755 -d "${pkgdir}/var/lib/xkb"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
